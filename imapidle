#!/usr/bin/env expect

#log_user 0

set mode [lindex $argv 0]
set host [lindex $argv 1]
set chan [lindex $argv 2]

if {$mode == "tls"} {
    spawn openssl s_client -quiet -tls1_2 -connect $host:143 -starttls imap
} else {
    spawn openssl s_client -quiet -tls1_2 -connect $host:993
}
set openssl $spawn_id

expect {
    -re "(\[^ \]+) OK.*\r" {
        send_user "Connected\n"
    }
    timeout {
        send_user "connect to $host timed out\n"
        exit
    }
    eof {
        send_user "EOF from $host\n"
        exit
    }
}

send ". CAPABILITY\r"
expect -re "\\* CAPABILITY (\[^\r\]+)\r"

set capability $expect_out(1,string)
regsub " LOGINDISABLED" $capability "" capability
regsub " IDLE" $capability "" capability
regsub -all " AUTH=\[^ \]+" $capability "" capability

spawn mbsync -q -q $chan
set mbsync $spawn_id

send "* OK\r"
expect "* OK\r\n"

set username UNKNOWN
set password UNKNOWN
expect {
    -re "(\[^ \]+) CAPABILITY\r" {
        set id $expect_out(1,string)
        expect *
        send "* CAPABILITY $capability\r"
        expect "* CAPABILITY $capability\r\n"
        send "$id OK\r"
        expect "$id OK\r\n"
        exp_continue
    }
    -re "(\[^ \]+) LOGIN \"(.*)\" \"(.*)\"\r" {
        set id $expect_out(1,string)
        set username $expect_out(2,string)
        set password $expect_out(3,string)
        send "$id OK\r"
        expect "$id OK\r\n"
    }
}

interact -u $openssl
